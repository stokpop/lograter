import org.gradle.api.plugins.internal.DefaultJavaPluginExtension

plugins {
    id 'java'
    id 'java-library'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
    id 'maven-publish'
    id 'signing'
    // check dependency updates: ./gradlew dependencyUpdates -Drevision=release
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.apache.commons:commons-compress:1.20'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.xerial:sqlite-jdbc:3.30.1'
    compile 'com.beust:jcommander:1.78'
    compile 'joda-time:joda-time:2.10.6'
    compile 'net.sf.jchart2d:jchart2d:3.3.2'
    compile 'org.hdrhistogram:HdrHistogram:2.1.12'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
    compile 'net.jcip:jcip-annotations:1.0'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'com.healthmarketscience.jackcess:jackcess:3.0.1'
    compile 'com.opencsv:opencsv:5.1'
    testCompile 'junit:junit:4.13'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

group = 'nl.stokpop'
version = '1.3.4-SNAPSHOT'
description = 'LogRater'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = "UTF-8"
}
javadoc {
    options.encoding = "UTF-8"
}

task customFatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'nl.stokpop.lograter.LogRater'
    }
    archiveBaseName = 'lograter-exec'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
        }
    }
}

license {
    header = file("src/main/resources/META-INF/LICENSE.txt")
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Peter Paul Bakker, Stokpop Software Solutions'
    exclude "**/*.txt"
    mapping {
        // the standard java style makes intellij complain about dangling javadoc
        java = "SLASHSTAR_STYLE"
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

if (project.hasProperty('release')) {

    subprojects {

        task javadocJar(type: Jar) {
            archiveClassifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            archiveClassifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    String repositoryUrl
                    if (version.endsWith("SNAPSHOT")) {
                        repositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                    }
                    else {
                        repositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    }
                    repository(url: repositoryUrl) {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/stokpop/lograter.git'

                        scm {
                            connection 'scm:git:https://github.com/stokpop/lograter.git'
                            developerConnection 'scm:git:git@github.com:stokpop/lograter.git'
                            url 'https://github.com/stokpop/lograter.git'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'stokpop'
                                name = 'Peter Paul Bakker'
                                email = 'peter.paul.bakker@stokpop.nl'
                            }
                        }
                    }
                }
            }
        }

    }
}

nexusStaging {
    username = project.hasProperty("ossrhUser") ?: "nobody"
    password = project.hasProperty("ossrhPassword") ?: "nopassword"
}

processResources {
    def props=["version":version]
    filesMatching('lograter.properties') {
        expand(props)
    }
}